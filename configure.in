#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)
AC_INIT([src/mywins/colordlg.c])

mGUtils_MAJOR_VERSION=1
mGUtils_MINOR_VERSION=0
mGUtils_MICRO_VERSION=3
mGUtils_INTERFACE_AGE=0
mGUtils_BINARY_AGE=0
mGUtils_VERSION=$mGUtils_MAJOR_VERSION.$mGUtils_MINOR_VERSION.$mGUtils_MICRO_VERSION

AC_SUBST(mGUtils_MAJOR_VERSION)
AC_SUBST(mGUtils_MINOR_VERSION)
AC_SUBST(mGUtils_MICRO_VERSION)
AC_SUBST(mGUtils_INTERFACE_AGE)
AC_SUBST(mGUtils_BINARY_AGE)
AC_SUBST(mGUtils_VERSION)

# libtool versioning
LT_RELEASE=$mGUtils_MAJOR_VERSION.$mGUtils_MINOR_VERSION
LT_CURRENT=`expr $mGUtils_MICRO_VERSION - $mGUtils_INTERFACE_AGE`
LT_REVISION=$mGUtils_INTERFACE_AGE
LT_AGE=`expr $mGUtils_BINARY_AGE - $mGUtils_INTERFACE_AGE`

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AC_CANONICAL_SYSTEM

dnl ========================================================================
# Checks for programs.
dnl AC_PROG_CC
AC_PROG_LD
AC_PROG_RANLIB
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AM_INIT_AUTOMAKE(libmgutils,$mGUtils_VERSION,no-define)
dnl ========================================================================
dnl Put the name and version of the package here

# Checks for libraries.

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([fcntl.h stdlib.h string.h sys/ioctl.h sys/time.h termio.h termios.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
AC_FUNC_STAT
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([bzero dup2 getcwd memmove memset putenv strchr strdup strerror strstr])

AC_ARG_WITH(libsuffix,
  [  --with-libsuffix=ARG    configure the suffix of the library name.])

if test "x$with_libsuffix" = "x"; then
  LIB_SUFFIX=""
else
  LIB_SUFFIX="_$with_libsuffix"
fi

have_libminigui="no"
AC_CHECK_HEADERS(minigui/common.h, have_libminigui=yes, foo=bar)

dnl ========================================================================
dnl check for run-time mode of MiniGUI
dnl ========================================================================
threads_version="no"
AC_CHECK_DECLS(_MGRM_THREADS, threads_version="yes", foo=bar, [#include <minigui/common.h>])

procs_version="no"
AC_CHECK_DECLS(_MGRM_PROCESSES, procs_version="yes", foo=bar, [#include <minigui/common.h>])

standalone_version="no"
AC_CHECK_DECLS(_MGRM_STANDALONE, standalone_version="yes", foo=bar, [#include <minigui/common.h>])

if test "x$threads_version" = "xyes"; then
    CFLAGS="$CFLAGS -D_REENTRANT"
    APP_LIBS="$APP_LIBS -lminigui_ths -lpthread"
else
    if test "x$standalone_version" = "xyes"; then
        APP_LIBS="$APP_LIBS -lminigui_sa"
    else
        APP_LIBS="$APP_LIBS -lminigui_procs"
    fi  

    lib_pthread="yes"
    AC_CHECK_DECLS(_MGGAL_SHADOW, APP_LIBS="$APP_LIBS -lpthread", lib_pthread="no", [#include <minigui/common.h>])
    if test "x$lib_pthread" = "xno"; then
        AC_CHECK_DECLS(_MGGAL_MLSHADOW, APP_LIBS="$APP_LIBS -lpthread", no_pthread="no", [#include <minigui/common.h>])
    fi
fi

CFLAGS="$CFLAGS -D__MGUTILS_LIB__"

AC_CHECK_DECLS(_HAVE_MATH_LIB, APP_LIBS="$APP_LIBS -lm", foo=bar, [#include <minigui/common.h>])
AC_CHECK_DECLS(_MGIMAGE_PNG, APP_LIBS="$APP_LIBS -lpng", foo=bar, [#include <minigui/common.h>])
AC_CHECK_DECLS(_MGIMAGE_JPG, APP_LIBS="$APP_LIBS -ljpeg", foo=bar, [#include <minigui/common.h>])
AC_CHECK_DECLS(_MGFONT_TTF, APP_LIBS="$APP_LIBS -lttf", foo=bar, [#include <minigui/common.h>])
AC_CHECK_DECLS(_MGFONT_FT2, APP_LIBS="$APP_LIBS -lfreetype", foo=bar, [#include <minigui/common.h>])

AM_CONDITIONAL(MGRM_THREADS, test "x$threads_version" = "xyes")
AM_CONDITIONAL(MGRM_PROCESSES, test "x$procs_version" = "xyes")
AM_CONDITIONAL(MGRM_STANDALONE, test "x$standalone_version" = "xyes")
AM_CONDITIONAL(MGRM_STANDALONE, test "x$standalone_version" = "xyes")

if test "$ac_cv_prog_gcc" = "yes"; then
    CFLAGS="$CFLAGS -Wall -Wstrict-prototypes -pipe"
fi

AC_SUBST(LIB_SUFFIX)
AC_SUBST(APP_LIBS)

AC_OUTPUT([Makefile
         include/Makefile
         src/Makefile
         src/mywins/Makefile
         src/vcongui/Makefile
         src/skin/Makefile
         src/templates/Makefile
         samples/Makefile
         samples/mginit/Makefile
         samples/mginit/res/Makefile
         samples/templates/Makefile
         samples/mywins/Makefile
         samples/skin/Makefile
         samples/notebook/Makefile
         samples/notebook/res/Makefile
         samples/painter/Makefile
         samples/picview/Makefile
         samples/picview/res/Makefile
         samples/vcongui/Makefile
         m4/Makefile])

if test "x$have_libminigui" != "xyes"; then
    AC_MSG_WARN([
        MiniGUI is not properly installed on the system. You need 
        MiniGUI Ver 3.0.x or later for building this package. 
        Please configure and install MiniGUI Ver 3.0.x first.
    ])
fi

